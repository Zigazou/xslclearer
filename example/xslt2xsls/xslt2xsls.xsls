// Transform XSL Template into XSLS format
stylesheet(
    version="1.0",
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
) {
    output(method="text", encoding="utf-8");

    // Replace string in string
    template("replace") {
        param $value, $search, $replace;

        if("contains($value,$search)") {
            value-of("concat(substring-before($value,$search),$replace)");

            call-template("replace") {
                with-param $value = "substring-after($value,$search)",
                           $search = "$search",
                           $replace = "$replace";
            }
        }

        if("not(contains($value,$search))") {
            value-of("$value");
        }
    }

    template("escapeString") {
        param $string;

        $string = {
            call-template("replace") {
                with-param $value = "$string", $search = "\"", $replace = "\\\"";
            }
        }

        $string = {
            call-template("replace") {
                with-param $value = "$string", $search = "\\", $replace = "\\\\";
            }
        }
    }

    // Special template for the xsl:variable tag (shortcut)
    template(match="xsl:variable") {
        choose() {
            // $variable = value;
            when("string-length(@select) > 0") {
                [$]value-of("@name");[ = "]value-of("@select");[";]
            }
            
            // $variable = { program }
            otherwise() {
                [$]value-of("@name");[ = {]
                    apply-templates();
                [}]
            }
        }
    }

    // Shortcut for for-each tag
    template(match="xsl:for-each") {
        [for-each("]value-of("@select");[") {]
            apply-templates();
        [}]
    }

    // Shortcut for when tag
    template(match="xsl:when") {
        [when("]value-of("@test");[") {]
            apply-templates();
        [}]
    }

    // Shortcut for if tag
    template(match="xsl:if") {
        [if("]value-of("@test");[") {]
            apply-templates();
        [}]
    }

    // Shortcut for call-template tag
    template(match="xsl:call-template") {
        choose() {
            // $variable = value;
            when("*") {
                [call-template("]value-of("@name");[") {]
                    apply-templates();
                [}]
            }
            
            // $variable = { program }
            otherwise() {
                [call-template("]value-of("@name");[");]
            }
        }
    }

    // Shortcut for param tag
    template(match="xsl:param") {
        choose() {
            // param $variable = { program };
            when("*") {
                [param $]value-of("@name");[ = {]
                    apply-templates();
                [}]
            }

            // param $variable = value;
            when("@select") {
                [param $]value-of("@name");[ = "]value-of("@select");[";]
            }

            otherwise() {
                [param $]value-of("@name");[;]
            }
        }
    }

    // Shortcut for with-param tag
    template(match="xsl:with-param") {
        choose() {
            // param $variable = { program };
            when("*") {
                [with-param $]value-of("@name");[ = {]
                    apply-templates();
                [}]
            }

            // param $variable = value;
            when("@select") {
                [with-param $]value-of("@name");
                [ = "]call-template("escapeString") { with-param $string = "@select"; }[";]
            }

            otherwise() {
                [with-param $]value-of("@name");[;]
            }
        }
    }

    // Template for every other xsl tag
    template(match="xsl:*") {
        value-of("substring-after(name(), 'xsl:')");
        [(]call-template("attributes");[)]
        choose() {
            when("*") {
                [ {]
                    apply-templates();
                [}]
            }
            otherwise() {
                [;]
            }
        }
    }

    // Handle every other tag
    template(match="*") {
        $tag = {
            value-of("name()");
            for-each("@*") {
                value-of("concat(' ', name(), '=\"', ., '\"')");
            }
        }

        [[&lt;]value-of("$tag");[&gt;\]]
        apply-templates();
        [[&lt;/]value-of("name()");[&gt;\]]
    }

    // Handle text
    template(match="*/text()") {
        choose() {
            when("string-length(normalize-space(.)) > 0") {
                [[]value-of(".");[\]]
            }
            otherwise() {
                value-of(".");
            }
        }
    }

    // Template for attributes
    template("attributes") {
        $attributes = {
            for-each("@*") {
                value-of("concat(name(), '=\"', ., '\", ')");
            }
        }

        // Delete the last comma
        if("string-length($attributes) > 0") {
            value-of("substring($attributes, 0, string-length($attributes) - 1)");
        }
    }
    
    // Template for comments
    template(match="comment()");
}
